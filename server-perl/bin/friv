#!/usr/bin/perl

=begin TODO

* Fix history browsing so that the cursor snaps back to the end when ya done.

=end TODO

=cut

use warnings;
use strict;

use POE;
use POE::Wheel::ReadLine;
use JavaScript::SpiderMonkey;
use Data::JavaScript::Anon;
use Getopt::Std;
use YAML;
use LWP;
use File::Spec;
use Archive::Zip::MemberRead;

my %opts;
getopts('u:h:p:r:o:d:f:b:', \%opts);

foreach (qw( user host password )) {
    my $letter = substr( $_, 0, 1 );
    unless (defined $opts{$letter}) {
        my $answer = '';
        until ( $answer ) {
            print "$_: ";
            chomp( $answer = <STDIN> );
        }
        $opts{ $letter } = $answer;
    }
}

################
# GLOBALS
################
our @cmd_hist = eval { @{ YAML::LoadFile("client.yaml") } };
my $next_rpc_request_id = 0;
my $last_message_recipient;
my $working_dir = $opts{f} || File::Spec->catdir($ENV{HOME}, ".friv");
my $ui_dir = File::Spec->catdir($working_dir, "ui");

# Make sure the working dir exists and is usable.
# Then chdir into it.
unless (-e $working_dir) {
    foreach ($working_dir, $ui_dir) {
	mkdir $_ or die "Tried to create working directory $_ but failed: $!";
    }
    
}
chdir $working_dir or die "I can't chdir to working directory $working_dir: $!";

# Crank up the POE session that will manage our Jabber connection.
POE::Session->create(
                     inline_states => {
                                       _start => \&start,
                                       user_input => \&user_input,
                                      }
                    );

POE::Kernel->run;

sub start {
    my($session,$heap) = @_[SESSION,HEAP];
    $heap->{'user'} = POE::Wheel::ReadLine->new(
        InputEvent => 'user_input',
        PutMode => 'immediate',
        );
    foreach (@cmd_hist) {
        $heap->{'user'}->addhistory($_);
    }
    $heap->{'user'}->get( 'Volity> ' );
    $heap->{username} = $opts{u};
    $heap->{resource} = $opts{r} || 'friv';
    $heap->{port} = $opts{o} || 5222;
    $heap->{host} = $opts{h};
    my $user = Volity::User->new(
                                 {
                                  host=>$heap->{host},
                                  password=>$opts{p},
                                  resource=>$heap->{resource},
                                  port=>$heap->{port},
                                  alias=>$heap->{resource},
                                  user=>$heap->{username},
				  poe_heap=>$heap,
				  bookkeeper_jid=>$opts{b} || 'bookkeeper@volity.net/volity',
                                 }
                                );
    $heap->{user_obj} = $user;
    $user->terminal($heap->{user});
    $user->kernel->run;
}

sub is_jid {
  my ($jid) = @_;
  if ($jid =~ /^[\w-]+@[\w-]+(?:\.[\w-]+)*(?:\/[\w-]+)?/) {
    return $jid;
  } else {
    return;
  }
}

sub check_jid {
  my ($pipe, $jid) = @_;
  if (is_jid($jid)) {
    return $jid;
  } else {
    $pipe->put("The string '$jid' doesn't look like a JabberID to me!");
    $pipe->put("Command ignored.");
    return;
  }
}

END {
    YAML::DumpFile("client.yaml",\@cmd_hist);
}

######################
# Input hanlding
######################

sub user_input {
    my($heap,$input,$exception) = @_[HEAP, ARG0, ARG1];
    my $user = $heap->{user_obj};
    if (defined $input) {
        $heap->{'user'}->get('Volity> ');
        my($cmd,@args) = tokenize($input,{});
        $cmd = lc( $cmd );

        return if not(defined($cmd)) or $cmd eq '';

        $heap->{'user'}->addhistory($input);
        push(@cmd_hist,$input);

        if (my $sub = __PACKAGE__->can("cmd_$cmd")) {
            &$sub(@_[0..(ARG0-1)],@args);
            return;
        }
        if ($heap->{'commands'}) {
           foreach (@{ $heap->{'commands'} }) {
               if ( $$_[0] eq $cmd ) {
                   js_call( $user->js, "client.$cmd", @args );
                   return;
               }
           }
        }
        $heap->{'user'}->put("Unknown command: $input");
    } else {
        $heap->{'user'}->put("Exception: $exception");
        exit;
    }
}

sub js_call {
    my( $js, $cmd, @args ) = @_;
    my $script = "$cmd(";
    foreach (@args) {
        next if /^-?\d*(?:\.\d+)?$/;
	next if ref($_);
        s/'/\\'/g;
        $_ = "'$_'";
    }
    # Convert the @args into a list of JS-safe strings.
    my @js_args;
    for my $arg (@args) {
	if (ref($arg)) {
	    push (@js_args, Data::JavaScript::Anon->anon_dump($arg));
	} else {
	    push (@js_args, $arg);
	}
    }
    $script .= join ', ', @js_args;
    $script .= ")";
    return $js->eval( $script );
}

sub tokenize {
    my($str,$vars) = @_;
    my @tokens;
    my $tok_count = 0;
    my $last = 0;
    while ($str =~ /\G(?:"([^"]+)(?<!\\)"|'([^']+)(?<!\\)'|((?:[^\s"']|(?<=\\)'|(?<=\\)"|(?<=\\)\s)+)|((?:(?<!\\)\s)+))/g) {
        $last = pos($str);
        my($dq,$sq,$nq,$sp) = ($1,$2,$3,$4);
        my $val = $dq||$sq||$nq||$sp;
        if ($dq or $nq) {
            $val = expand($val,$vars);
        } else {
            $val = unescape($val);
        }
        if ($sp) {
            $tok_count ++ if $#tokens == $tok_count;
        } else {
            $tokens[$tok_count] .= $val;
        }
    }
    if ($last < length($str)) {
        $tokens[$tok_count] .= substr($str,$last);
    }
    return @tokens;
}

sub unescape {
    my($str) = @_;
    $str =~ s/\\(.)/$1/g;
    return $str;
}

sub expand {
    my($str,$vars) = @_;
    $str =~ s{
            (?:
            (?<!\\)\$(\w+) |
            \\(.)
            )
        }{
            if ($1) {
                $$vars{$1};
            } else {
                $2
            }
        }gxe;
    $str = unescape($str);
    return $str;
}

###########################
# User commands
###########################

sub cmd_info {
    my($heap) = $_[HEAP];
    no warnings qw(uninitialized);
    my $user = $heap->{user_obj};
    $heap->{'user'}->put($_) for
      "Status information:",
      "  JID: ".$user->jid,
      "  Nickname: ".$user->nick,
      "  Table JID: ".$user->table_jid,
      "  Referee JID: ".$user->referee_jid;
}

sub cmd_quit {
    my($heap) = $_[HEAP];
    $heap->{'user'}->put('Be seeing you.');
    $heap->{user_obj}->send_presence({type=>'unavailable'});
    exit;
}

sub cmd_invite {
    my ($heap, $invited) = @_[HEAP, ARG0];
    my $user = $heap->{user_obj};
    unless ($user->referee_jid) {
	$user->print_message("Create or join a table first!");
	return;
    }
    unless ($invited) {
	$user->print_message("Usage: invite [jid]");
	return;
    }
    my $rpc_id = ++$next_rpc_request_id;
    $user->make_rpc_request(
			    {
				to=>$user->referee_jid,
				id=>$rpc_id,
				methodname=>'volity.invite_player',
				args=>[$invited],
				handler=>'invite_player',
			    }
			    );

}

sub cmd_accept {
    my ($heap) = $_[HEAP];
    my $user = $heap->{user_obj};
    if (my $invitation = $user->invitation) {
	$user->current_command('accept_invitation');
	$user->discover_uis_for_ruleset($$invitation{ruleset});
    }
}

sub cmd_nick {
    my($heap,$nickname) = @_[HEAP,ARG0..$#_];
    unless ($nickname) {
        $heap->{'user'}->put("Form: nick <nickname>");
        return;
    }
    $heap->{'nick'} = $nickname;
    $heap->{user_obj}->nick($nickname);
    $heap->{'user'}->put("Nickname set.");
}

sub cmd_debug {
  my ($heap) = $_[HEAP];
  my $user = $heap->{user_obj};
  $heap->{user}->put("Current user object: $user");
  if ($user->referee_jid) {
      $heap->{user}->put("Current ref JID: " . $user->referee_jid);
  }
}

sub cmd_help {
  my($heap,$on) = @_[HEAP,ARG0..$#_];
  $heap->{'user'}->put("$_") for split /\n/, <<CMDLIST;
Volity client commands:
  msg (JID) (text) - send private message to the given JID
  tmsg (text)      - send groupchat message to the current table
  rmsg (text)      - send private message to the current referee

  nick (nickname)  - set a new table nickname for yourself
  roster           - view your roster

  load_ui (path)   - manually load a UI file
  clear_ui         - clear the current UI file from memory

  new_table (JID)  - request a new table from the given server JID
  join_table (JID) - join an existing table
  invite (JID)     - invite another player to the table
  accept           - accept the last invitation you received
  add_bot          - request a new bot for the current table
  start_game       - request a new game at the current table

  info             - see some info about your current status
  help             - this list
  quit             - log off from Volity and exit this program
CMDLIST
  return unless $heap->{'commands'};
  $heap->{'user'}->put( "" );
  foreach (@{ $heap->{'commands'} }) {
    $heap->{'user'}->put( sprintf("  %-16s - %s",$$_[0],$$_[1]) );
  }
}

sub cmd_clear_ui {
    my $heap = $_[HEAP];
    my $user = $heap->{user_obj};
    if (defined($user->js)) {
	$user->js(undef);
	$user->print_message("OK, UI cleared.");
    } else {
	$user->print_message("Hmm, there wasn't any UI set.");
    }
}

sub cmd_load_ui {
  my ($heap, $filename) = @_[HEAP, ARG0];

  my $term = $heap->{'user'};
  
  unless (open FILE, $filename) {
      $term->put("Couldn't open $filename: $!");
      return;
  }
  my $source = join '', <FILE>;
  close FILE;

  $heap->{user_obj}->load_js_from_source($source);
  $heap->{user_obj}->manual_ui_load(1);

}

sub js_writeln {
    my ($term, @output) = @_;
    $term->put( "@output" );
}

sub js_rpc {
    my ($user, $cmd, @args) = @_;
    
    $user->make_rpc_request(
                            {
                             to=>$user->referee_jid,
                             id=>"$cmd.".time,
                             methodname=>"game.$cmd",
			     args=>\@args,
                             handler=>sub {},
                            }
                           );
}

sub js_registerCommand {
    my( $commands, $command, $shortdesc, $longdesc ) = @_;
    push @$commands, [ $command, $shortdesc, $longdesc ];
}

sub cmd_new_table {
  my ($heap, $server_jid) = @_[HEAP, ARG0];
  my $user = $heap->{user_obj};
  unless (defined($server_jid)) {
    $heap->{user}->put("Usage: new_table [game-server-jid]");
    return;
  }
  unless (is_jid($server_jid)) {
    $heap->{user}->put("That doesn't look like a JID to me!");
    return;
  }
  
  $user->server_jid($server_jid);
  $user->current_command('new_table');
  if ($user->js and $user->manual_ui_load) {
      $user->print_message("UI file manually loaded, so I won't fetch another over the network.");
      $user->print_message("To force a fetch, use the clear_ui command, then rejoin this table.");
      $user->request_new_table($server_jid);
  } else {
      $user->print_message("Requesting UI file from the Volity network.");
      $user->manual_ui_load(undef);
      $user->request_disco_info({to=>$server_jid, id=>"server"});
  }
}

sub cmd_add_bot {
  my $user = $_[HEAP]->{user_obj};
  my $rpc_id = ++$next_rpc_request_id;
  $user->make_rpc_request(
                          {
                           to=>$user->referee_jid,
                           id=>$rpc_id,
                           methodname=>'volity.add_bot',
                           handler=>'add_bot',
                          }
                         );
}

sub cmd_join_table {
  my $user = $_[HEAP]->{user_obj};
  my $table_jid = $_[ARG0];
  return unless check_jid($_[HEAP]->{user}, $table_jid);
  if ($user->js and $user->manual_ui_load) {
      $user->print_message("UI file manually loaded, so I won't fetch another over the network.");
      $user->print_message("To force a fetch, use the clear_ui command, then rejoin this table.");
      $user->join_table($table_jid);
  } else {
      $user->print_message("You must load a UI file (with the load_ui command) before joining a table.");
      $user->print_message("(Friv does not yet support automatic UI fetching outside of table creation.)");
  }
}

sub cmd_tmsg {
  my ($user, @message) = ($_[HEAP]->{user_obj}, @_[ARG0..$#_]);
  my $message = join(' ', @message);
  unless (defined($user->table_jid)) {
    $user->print_message("Can't send a message to the table, since you're not at a table right now.");
    return;
  }
  unless (defined($message)) {
    $user->print_message("Beg your pardon? (Usage: tmsg [message-text])");
    return;
  }
  $user->send_message(
                      {
                       type=>'groupchat',
                       body=>$message,
                       to=>$user->table_jid,
                      }
                     );
}

sub cmd_msg {
  my ($user, $to, @message) = ($_[HEAP]->{user_obj}, $_[ARG0], @_[ARG1..$#_]);
  my $message = join(' ', @message);
  unless (defined($message)) {
    $user->print_message("Beg your pardon? (Usage: msg [to-jid] [message-text])");
    return;
  }
  return unless check_jid($user->terminal, $to);
  $user->send_message(
                      {
                       type=>'normal',
                       body=>$message,
                       to=>$to,
                      }
                     );
}

sub cmd_start_game {
  my $user = $_[HEAP]->{user_obj};
  unless (defined($user->table_jid)) {
    $user->print_message("Can't start a new game, because you're not seated at a table.");
    return;
  }
  my $rpc_id = ++$next_rpc_request_id;
  $user->make_rpc_request(
                          {
                           id=>$rpc_id,
                           to=>$user->referee_jid,
                           methodname=>'volity.start_game',
                           handler=>'start_game',
                          }
                         );
}

sub cmd_rmsg {
  my ($user, @message) = ($_[HEAP]->{user_obj}, @_[ARG0..$#_]);
  my $message = join(' ', @message);
  unless (defined($user->referee_jid)) {
    $user->print_message("Can't send a message to the referee, since you're not at a table right now.");
    return;
  }
  unless (defined($message)) {
    $user->print_message("Beg your pardon? (Usage: rmsg [message-text])");
    return;
  }
  $user->send_message(
                      {
                       type=>'normal',
                       body=>$message,
                       to=>$user->referee_jid,
                      }
                     );
}

sub cmd_roster {
  my $user = $_[HEAP]->{user_obj};
  my $roster = $user->roster;
  my @jids = sort($roster->jids);
  my @available_jids; my @unavailable_jids;
  for my $jid (@jids) {
    my @presence_list = $roster->presence($jid);
    if (@presence_list and grep(((not(defined($$_{type}))) or ($$_{type} ne 'unavailable')), @presence_list)) {
      push (@available_jids, $jid);
    } else {
      push (@unavailable_jids, $jid);
    }
  }
  $user->print_message("----AVAILABLE----");
  if (@available_jids) {
    for my $jid (@available_jids) {
#      $user->print_message($_);
      for my $presence ($roster->presence($jid)) {
        my $full_jid = $jid;
        if (defined $$presence{'resource'}) {
            $full_jid .= "/$$presence{resource}";
        }
        $user->print_message( $full_jid );
      }
    }
  } else {
    $user->print_message("Nobody in your roster is online.");
  }
  if (@unavailable_jids) {
    $user->print_message("----UNAVAILABLE----");
    foreach (@unavailable_jids) {
      $user->print_message($_);
    }
  }
}

package Volity::User;

use warnings;
use strict;

use base qw(Volity::Jabber);
use fields qw(nick terminal poe_heap table_jid referee_jid server_jid js opponents available_uis current_ui_url bookkeeper_jid uis_to_check current_command manual_ui_load invitation);
use POE;

sub init_finish {
  my $self = $_[OBJECT];
  
  my $jid = $_[ARG0];
  
  # Succesful login!
  $self->print_message("Connection successful. Weclome to Volity.");
  $self->request_roster;
  $self->current_ui_url({});
}

sub receive_roster {
  my $self = shift;
  $self->SUPER::receive_roster(@_);
  # Having built our roster, we'll now announce our presence to the server.
  $self->send_presence;
}

sub print_message {
  my $self = shift;
  my ($message) = @_;
  $self->terminal->put($message);
}

sub rpc_response_new_table {
  my $self = shift;
  my( $message ) = @_;
  my $table_jid = $$message{'response'};
  $self->join_table($table_jid);

  my ($ref_resource) = $table_jid =~ /^(.*?)@/;
  my $ref_jid = $$message{'from'};
  $self->print_message("New table made, and joined. (Table JID: $table_jid)");
}

sub _rpc_receive_invitation {
    my $self = shift;
    my ($sender_jid, $invitation) = @_;
    $self->print_message("$sender_jid has invited you to a game!");
    if ($invitation->{message}) {
	$self->print_message("This message was attached:");
	$self->print_message($invitation->{message});
    }
    $self->print_message("Use the 'accept' command to accept the invitation.");
    $self->invitation($invitation);
    return 1;
}


sub fetch_ui_file {
    my $self = shift;
    my ($url) = @_;

    my $agent = LWP::UserAgent->new;

    # Now, at this point, we can do sexy things, like look at what we already
    # might have cached from previous forays to this URL.
    # Today, no sexiness. We will grab the URI file blindly and crush
    # whatever we last downloaded with the same name.
    # Sexiness tomorrow, maybe.
    my $request = HTTP::Request->new(GET=>$url);
    my $response = $agent->request($request);
    my $local_zipfile = File::Spec->catfile($working_dir, "ui.zip");
    open (ZIP, ">$local_zipfile") or die "Can't write ui zipfile $local_zipfile: $!\n";
    print ZIP $response->content;
    close (ZIP) or die "Can't close zipfile $local_zipfile: $!\n";
    my $zip_reader;
    unless ($zip_reader = Archive::Zip->new($local_zipfile)) {
	$self->print_message("Oh, there was a problem reading the zipfile $local_zipfile. Sorry.");
	return;
    }
    my $js_fh;
    my @js_source;
    if ($js_fh = Archive::Zip::MemberRead->new($zip_reader, "main")) {
	while (defined(my $line = $js_fh->getline)) { 
	    push (@js_source, $line);
	}
    } else {
	$self->print_message("The archive $local_zipfile doesn't seem to have a main file. I can't use it. Sorry...");
	return;
    }
    
    # Having done all that, create the JS interpreter.
    
    $self->load_js_from_source(join("\n", @js_source));

    # Now make the actual RPC request.

    if ($self->current_command eq 'new_table') {
	$self->request_new_table($self->server_jid);
    } elsif ($self->current_command eq 'accept_invitation') {
	$self->join_table($self->invitation->{table});
    }

}

sub request_new_table {
    my $self = shift;
    my ($server_jid) = @_;
    my $rpc_id = 'new-table';
    $self->make_rpc_request(
			    {
				to=>$self->server_jid,
				id=>$rpc_id,
				methodname=>'volity.new_table',
				handler=>'new_table',
			    }
			    );
}

sub join_table {
    my $self = shift;
    my ($table_jid) = @_;
    $self->clear_opponent_nicknames;
    $self->join_muc(
                  {
                   jid=>$table_jid,
                   nick=>$self->nick || $self->user,
                  }
                 );
  $self->table_jid($table_jid);
}

sub load_js_from_source {
    my $self = shift;
    my ($js_source) = @_;
    my $commands = $self->poe_heap->{commands} ||= [];

    my $js = JavaScript::SpiderMonkey->new();
    $js->init();
    my $game = $js->object_by_path("game");
    my $referee = $js->object_by_path("client");
    my $info = $js->object_by_path("info");
    $js->function_set("writeln", sub { main::js_writeln( $self->terminal, @_) } );
    $js->function_set("rpc", sub { main::js_rpc( $self, @_ ) } );
    $js->function_set("registerCommand", 
		      sub { main::js_registerCommand( $commands, @_ ) } , $referee );
    
    $js->eval( $js_source );

    $js->eval( "info.nickname = '" . ($self->nick || $self->user) . "'" );
    $js->eval( "info.opponents = {}" );
    for my $nickname ($self->opponent_nicknames) {
	$js->eval( "info.opponents['$nickname'] = {}" );
    }
    
    if ($self->js) {
	$self->js->destroy;
    }
    $self->js( $js );
}

sub rpc_response_add_bot {
  my $self = shift;
  my( $message ) = @_;
  $self->print_message("Response from the bot-add request was: $$message{'response'}");
}

sub rpc_response_invite_player {
    my $self = shift;
    $self->print_message("The invitation has been sent.");
}

sub rpc_response_start_game {
  my $self = shift;  
  $self->print_message("The game has begun. Good luck!");
}

sub rpc_response_default {
  my $self = shift;
  my ($message) = @_;
  $self->print_message("Received an RPC response with id ".
    "$$message{'id'}, but I don't know how to handle it.");
}

sub handle_rpc_transmission_error {
  my $self = shift;
  my ($iq, $code, $error_message) = @_;
  my $full_message = "Failed to send an RPC packet. Error: $code";
  if (defined($error_message)) {
    $full_message .= " - $error_message";
  }
  $self->print_message($full_message);
}

sub handle_rpc_fault {
  my $self = shift;
  my ($fault_hash) = @_;
  my $full_message = "Received an RPC fault response: ";
  $full_message .= join(' - ', $$fault_hash{fault_code}, $$fault_hash{fault_string});
  $self->print_message($full_message);
}

sub handle_rpc_request {
  my $self = shift;
  my ($req) = @_;
  my $sub;
  if ($$req{method} =~ /^volity\.(\w+)/) {
      my $rpc_method = $1;
      my $local_method = "_rpc_$rpc_method";
      my $return_value;
      if ($self->can($local_method)) {
	  my @return_values = $self->$local_method($$req{from}, @{$$req{args}});
	  if (@return_values < 2) {
	      $self->send_rpc_response($$req{from}, $$req{id}, @return_values);
	  } else {
	      $self->send_rpc_fault($$req{from}, $$req{id}, @return_values);
	  }
      } else {
	  $self->warn("Got bogus RPC request, '$$req{method}'");
      }
  } elsif ($$req{'method'} =~ /^game\.(\w+)/) {
      main::js_call( $self->js, $$req{'method'}, @{ $$req{'args'} } );
    } else {
	$self->print_message("Unknown rpc request namespace: $$req{'method'}");
    }
}

sub handle_normal_message {
  my $self = shift;
  my ($message) = @_;
  $self->terminal->put("From: $$message{from}");
  $self->terminal->put("$$message{body}");
}

sub handle_chat_message {
  my $self = shift;
  my ($message) = @_;
  $self->terminal->put("From: $$message{from}");
  $self->terminal->put("$$message{body}");
}

sub handle_groupchat_message {
  my $self = shift;
  my ($message) = @_;
  my ($sender_nickname) = $$message{from} =~ /\/(.*)$/;
  $sender_nickname ||= $$message{from};
  $$message{body} ||= '';
  $self->terminal->put("$sender_nickname: $$message{body}");
}

# This presence handler detects a table's referee through MUC attributes.
# It also watches for general presence updates, and updates the user's
# internal roster object as needed.
sub jabber_presence {
  my $self = shift;
  my ($node) = @_;
  my $x; # Hey, that's the name of the element, OK?
  if (defined($node->attr('type')) and $node->attr('type') eq 'error') {
    # Ruh roh. Just print an error message.
    my $error = $node->get_tag('error');
    my $code = $error->attr('code');
    $self->terminal->put("Got an error ($code):");
    my $message = $error->data || "Something went wrong.";
    $self->terminal->put($message);
    return;
  }
  if (($node->get_tag('x')) and (($x) = grep($_->attr('xmlns') eq "http://jabber.org/protocol/muc#user", $node->get_tag('x')))) {
    # Aha, someone has joined the table.
    my $new_person_jid = $x->get_tag('item')->attr('jid');
    my $affiliation = $x->get_tag('item')->attr('affiliation');
    if ($affiliation eq 'owner') {
      # This is the table's ref.
      $self->referee_jid($new_person_jid);
    } else {
      # This is a potential opponent!
      unless ($node->attr('type')) {
	# No 'type' attribute means they're joining us...
	# Save the nickname. We'll pass it to the UI file later.
	$self->add_opponent_nickname($node->attr('from'));
      } elsif ($node->attr('type') eq 'unavailable') {
	# Oh, they're leaving...
	$self->remove_opponent_nickname($node->attr('from'));
      }
    }
  } elsif ($self->roster->has_jid( $node->attr('from') )) {
    # Someone on our roster just changed their presence.
    my $roster = $self->roster;
    my $jid = $node->attr('from');
    my $presence_hash = {type=>$node->attr('type')};
    foreach (qw(show status priority)) {
      $$presence_hash{$_} = $node->get_tag($_)->data if $node->get_tag($_);
    }
    $roster->presence($jid, $presence_hash);
    my $prez = $roster->presence($jid);
  }
}

sub handle_disco_info {
    my $self = shift;
    my ($from, $id, $items, $fields) = @_;
    my @items = @$items;

    # The value of $id indicates what sort of Volity entity sent the packet.
    if ($id =~ /^ruleset-ui(\d+)$/) {
	my $id_number = $1;
	my $current_ui_url = $self->current_ui_url->{$id_number};
	my @types = @{$$fields{"client-type"}};
	if (grep($_ eq 'http://volity.org/protocol/ui/text', @types)) {
	    # Good, this one is available to us.
	    push (@{$self->{available_uis}},
		  {
		      url=>$current_ui_url,
		      description=>$$fields{description}->[0],
		      reputation=>$$fields{reputation}->[0],
		  },
		  );
	}
	$self->{uis_to_check} -= 1;
	unless ($self->uis_to_check) {
	    if (@{$self->available_uis} > 1) {
#		use Data::Dumper; die Dumper($self->available_uis);
		$self->show_ui_menu;
	    } elsif (@{$self->available_uis}) {
		my $ui = $self->available_uis->[0];
#		use Data::Dumper; die (Dumper($ui));
		$self->print_message("Fetching game UI file from $$ui{url}...");
		$self->fetch_ui_file($$ui{url});
	    } else {
		$self->print_message("No text UIs available for this game. Sorry.");
	    }
	}

    } elsif ($id eq 'server') {
	my ($ruleset_uri) = @{$$fields{ruleset}};
	my ($ruleset_version) = @{$$fields{"ruleset-version"}};

	$self->discover_uis_for_ruleset($ruleset_uri);
    } else {
	$self->terminal->("Um... I got a disco info response with id $id. I'm not sure what to do about it.");
    }
}

# discover_uis_for_ruleset: make a disco request for a given ruleset's
# ui-items.
# WARNING! The way this is presently coded, this plugs into a chain reaction
# that will end up in a UI being downloaded. Well... so be it.
sub discover_uis_for_ruleset {
    my $self = shift;
    my ($ruleset_uri) = @_;
    
    $self->request_disco_items({to=>$self->bookkeeper_jid,
				id=>'ruleset',
				node=>$ruleset_uri . "|uis",
			    });
}

sub handle_disco_items {
    my $self = shift;
    my ($from, $id, $items) = @_;
    my @items = @$items;
    
    if ($id eq 'ruleset') {
	$self->available_uis([]);
	$self->uis_to_check(scalar(@items));
	my $id_num = 0;
	for my $item (@items) {
	    my $iq_id = "ruleset-ui" . ++$id_num;
	    $self->current_ui_url->{$id_num} = $item->node;
	    $self->request_disco_info({to=>$item->jid, 
				  id=>$iq_id,
				  node=>$item->node,
			      });
	}
    }
}

# add_opponent_nickname: Add the given nickname (either a full JID in MUC
# format, or a bare string containing only the nickname) to our internal
# list of known opponents at the current table.
sub add_opponent_nickname {
  my $self = shift;
  my ($name) = @_;
  my $nickname;
  if (main::is_jid($name)) {
    ($nickname) = $name =~ /\/(.*)$/;
    unless ($nickname) {
      die "GACK. I couldn't tease a nickname out of the jid $name.";
    }
  } else {
    $nickname = $name;
  }
  my $my_nickname = $self->nick || $self->user;
  unless ($my_nickname eq $nickname) {
    $self->{opponents}{$nickname} = 1;
  }
}

sub remove_opponent_nickname {
  my $self = shift;
  my ($name) = @_;
  my $nickname;
  if (main::is_jid($name)) {
    ($nickname) = $name =~ /\/(.*)$/;
    unless ($nickname) {
      die "GACK. I couldn't tease a nickname out of the jid $name.";
    }
  } else {
    $nickname = $name;
  }
  return(delete($self->{opponents}{$nickname}));
}

sub opponent_nicknames {
  my $self = shift;
  return keys(%{$self->{opponents}});
}

sub clear_opponent_nicknames {
  my $self = shift;
  $self->{opponents} = {};
}

=head1 NAME

Friv - A simple text-only Volity client

=head1 SYNOPSIS

 $ friv -u myusername -p mypassword -h volity.net

 Connection successful. Weclome to Volity.
 Volity> new_table rps@volity.net/testing   
 Requesting UI file from the Volity network.
 Fetching game UI file from http://volity.org/games/rps/text/rps.zip...
 rps.js Loaded!
 New table made, and joined.
 Volity> add_bot
 Response from the bot-add request was: ok
 Volity> start_game
 The game has begun. Good luck!
 RPS begins!  Have fun and good luck!
 RandomBot: I have made my move.
 Volity> select rock
 Good old rock! Nothing beats rock.

 [etc....]

=head1 DESCRIPTION

This program provides a very simple text-based interface to the Volity
network. It will let you create or join tables at any game server, so
long as a text-based UI file for that server's game is available.

Friv is part of I<Frivolity>, the Perl implementation of the Volity
network game platform. While it aims to be a full-featured Volity
client, it is more geared towards testing and hacking than general
use. For a more user-friendly Volity experience, we recommend a
GUI-based client, such as Javolin.

To learn more about Volity, visit the Volity developers' webesite at
http://volity.org.

=head1 USAGE

To play a game, use the C<new_table> command (see L<"COMMANDS">) to
create a new Volity table. You can then call C<add_bot> to summon
automated players (if the server supports them), or have your friends
join through their own clients. (Friv does not support any sort of
invitation mechanism yet.)  When you are ready to start playing, call
C<start_game>.

Like all Volity clients, Friv requires an appropriate set of
user-interface files before you can play a game. When you request a
new table, Friv will automatically negotiate with the specified server
in an attempt to locate a text-friendly UI file to download and
use. If successful, it transparently sets up everything it needs and
escorts you to the new table. Otherwise, you'll receive an apologetic
message.

After joining a table, use the C<help> command to see both a summary
of Friv's global commands I<and> the commands specific to the game UI
currently laoded. For example, the "select" command shown in
L<"SYNOPSIS"> was defined by the rock-paper-scissors UI file that Friv
transparently loaded when that user created a table at a
rock-paper-scissors game server.

You can also use the C<join_table> command to join a game table that
someone else has already created. Unfortunately, Friv currently lacks
the ability to fetch UI files when joining tables in this fashion; you
will have to have your own local copy of that file, and C<load_ui> on
it before joining.

=head1 RUNNING

Friv takes the following flags on launch:

=head2 Required flags

These flags set information that Friv needs to authenticate you with a
Volity (Jabber) server.

If you don't supply these as command flags, the program will prompt
you for them. (Supplying your password in this way offers more
security than supplying it on the command line.)

=over

=item u

The account's username.

=item h

The account's host. Examples: volity.net, jabber.org.

=item p

The account's password.

=back

=head2 Optional flags

=over

=item b

The Volity bookkeeper JID to use. Defaults to
bookkeeper@volity.net. Unless you really know what you're doing, you
probably shouldn't change this.

=item f

The working directory that Friv can use to store its scratch
files. Defaults to "$ENV{HOME}/.friv".

=item o

The TCP port to connect to. Defaults to 5222 (Jabber's default port).

=item r

The resource to use with the Jabber account. Defaults to 'friv'.

=back

=head1 COMMANDS

=over

=item roster

View your Jabber roster, including indicators of who's online and who's not.

=item help

Displays a list of the standard Friv commands (as described in this
manpage), as well as any game-specific commands defined by the current UI
file (if there is one).

=item load_ui C<filename>

Loads an ECMAScript UI file from the local filesystem. Useful if
you're testing a new UI file, or otherwise have a file that isn't
available over the network. If you load a UI file manually,
C<new_table> won't attempt to fetch a replacement over the network
unless you C<clear_ui> first.

Once you load a UI file, several new game-specific commands may
before available. Use the C<help> command to see a list of them (along
with all the other standard Friv commands).

=item clear_ui

Clears the currently loaded UI file from memory.

=item new_table C<JID>

Attempt to create a new table at the specified JID. If successful, you
will immediately join the new table.

If you have not manually loaded a UI file (with C<load_ui>), Friv will
open negotiation with the specified server in order to find and load
an appropriate UI file by itself. It it cannot do so, you'll recieve
an explanatory error message, and no table will be created.

=item join_table C<JID>

Attempt to join the table (or Jabber groupchat) at the specifed JID.

Unfortunately, you must load a UI file manually (with C<load_ui>)
before calling this. Future versions of Friv will be able to fetch the
appropriate UI file over the network, much as with C<new_table>.

=item add_bot

Summon a bot to the current table. Only works if the referee offers a
single bot type. (Friv currently lacks the form-support needed to
choose new bots.)

=item invite C<JID>

Sends an invitation to the player with the given jid. Only works if you are already seated at a table.

=item accept

Accepts the last invitation you received, and brings you to the table.

=item start_game

Request that the referee at the current table start the game.

=item msg C<JID> C<Message body>

Send the given text (as a 'normal'-type Jabber message) to the specifed JID.

=item tmsg C<Message body>

Send the given text (as a 'groupchat'-type Jabber message) to the table.

=item rmsg C<Message body>

Send the given text to the table's referee.

=item quit

Quits the program (and logs you out of the system).

=back

=head1 BUGS

The most recent version of Friv represents the most advanced state of
the Volity platform. Since Volity is unfinished, so is Friv, and it
just about the same ways. On the other hand, Friv will continue to
develop so long as Volity does. Stay tuned.

=head1 AUTHOR

Jason McIntosh <jmac@jmac.org> (Jabber: jmac@volity.net)

=head1 COPYRIGHT

Copyright (c) 2004 by Jason McIntosh

=head1 SEE ALSO

L<Volity>

http://volity.org

=cut
					  
					  
